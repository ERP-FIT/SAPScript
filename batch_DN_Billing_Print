Global session As Object
Global SAPApp As Object
Global batch_mode As Boolean

Sub open_sap()
    If session Is Nothing Then
        Set SapGuiAuto = GetObject("SAPGUI")  'Get the SAP GUI Scripting object
        Set SAPApp = SapGuiAuto.GetScriptingEngine 'Get the currently running SAP GUI
        Set SAPCon = SAPApp.Children(0) 'Get the first system that is currently connected
        Set session = SAPCon.Children(0) 'Get the first session (window) on that connection
    End If
End Sub

Sub close_sap()
    If Not SAPApp Is Nothing Then
        Set session = Nothing
        Set SAPCon = Nothing
        Set SAPApp = Nothing
        Set SapGuiAuto = Nothing
        MsgBox "Process Completed"
    End If
End Sub
Sub clear_previous_status()
    last_row = ActiveSheet.UsedRange.Rows.Count
    If last_row > 2 Then
        Range("I2:O" + CStr(last_row)).Select
        Selection.ClearContents
    End If
    Range("A2").Select
End Sub
Sub batch_processing()
    Call open_sap
    batch_mode = True
    Call clear_previous_status
    Call batch_create_DN
    Call batch_PGI
    Call batch_create_billing
    Call batch_print
    Call close_sap
    batch_mode = False
End Sub

Sub update_sales_order_storage_location()
    If batch_mode = False Then Call open_sap
    last_row = ActiveSheet.UsedRange.Rows.Count
    For i = 2 To last_row
        If Cells(i, 2) <> "" And Cells(i - 1, 2) <> Cells(i, 2) Then
            session.findById("wnd[0]/tbar[0]/okcd").Text = "/nva02"
            session.findById("wnd[0]").sendVKey 0
            session.findById("wnd[0]/usr/ctxtVBAK-VBELN").Text = Cells(i, 2)
            session.findById("wnd[0]").sendVKey 0
            On Error Resume Next
                session.findById("wnd[1]").Close   'close info popup subsequence document exist
            If session.findById("wnd[0]/sbar").MessageType = "W" Then session.findById("wnd[0]").sendVKey 0
            sales_order = Cells(i, 2)
            For j = i To last_row
                If Cells(j, 2) <> sales_order Then Exit For
                session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_OVERVIEW/tabpT\02/ssubSUBSCREEN_BODY:SAPMV45A:4401/subSUBSCREEN_TC:SAPMV45A:4900/subSUBSCREEN_BUTTONS:SAPMV45A:4050/btnBT_POPO").press
                session.findById("wnd[1]/usr/txtRV45A-POSNR").Text = Cells(j, 3)
                session.findById("wnd[1]/tbar[0]/btn[0]").press
                On Error Resume Next
                    session.findById ("wnd[2]")
                If Err.Number = 0 Then
                    session.findById("wnd[2]").Close
                    Cells(i, 14) = "item not in sales order"
                Else
                    session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_OVERVIEW/tabpT\02/ssubSUBSCREEN_BODY:SAPMV45A:4401/subSUBSCREEN_TC:SAPMV45A:4900/tblSAPMV45ATCTRL_U_ERF_AUFTRAG/txtVBAP-POSNR[0,0]").SetFocus
                    session.findById("wnd[0]").sendVKey 2
                    session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_ITEM/tabpT\03").Select
                    session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_ITEM/tabpT\03/ssubSUBSCREEN_BODY:SAPMV45A:4452/ctxtVBAP-LGORT").Text = Cells(j, 6) '"r302"
                    session.findById("wnd[0]").sendVKey 0
                    On Error Resume Next  ' auto switch to ATP screen, accept the proposal to partial deliver
                        session.findById ("wnd[0]/usr/ctxtRV03V-ETDAT")
                    If Err.Number = 0 Then session.findById("wnd[0]/tbar[1]/btn[7]").press
                    session.findById("wnd[0]/tbar[0]/btn[3]").press
                End If
            Next j
            session.findById("wnd[0]/tbar[0]/btn[11]").press
            On Error Resume Next   'document is incomplete
                session.findById ("wnd[1]/usr/btnSPOP-VAROPTION1")
            If Err.Number = 0 Then
                Cells(i, 15) = session.findById("wnd[1]").PopupDialogText
                session.findById("wnd[1]/usr/btnSPOP-VAROPTION1").press
            End If
        End If
    Next i
    If batch_mode = False Then Call close_sap
End Sub

Sub batch_create_DN()
    If batch_mode = False Then Call open_sap
    Call clear_previous_status
    last_row = ActiveSheet.UsedRange.Rows.Count
    picking_view_selected = False
    For i = 2 To last_row
        If Cells(i, 2) = "" Then Exit For
        If Cells(i - 1, 2) = Cells(i, 2) Then
            Cells(i, 10) = Cells(i - 1, 10)   'copy delivery number to following items of the same order
        Else                                  'only process sales order one time, on the 1st item
            item_count = 0
            session.findById("wnd[0]").maximize
            session.findById("wnd[0]/tbar[0]/okcd").Text = "/nvl01n"
            session.findById("wnd[0]").sendVKey 0
            If session.findById("wnd[0]/usr/ctxtLIKP-VSTEL").Text = "" Then  'set default shipping point
                session.findById("wnd[0]/usr/ctxtLIKP-VSTEL").Text = "cn12"
            End If
            session.findById("wnd[0]/usr/ctxtLV50C-DATBI").Text = Cells(2, 8) 'Cells(i, 8) 'selection date "22.03.2022"
            session.findById("wnd[0]/usr/ctxtLV50C-VBELN").Text = Cells(i, 2) 'sales order
            session.findById("wnd[0]").sendVKey 0
            
            MessageType = session.findById("wnd[0]/sbar").MessageType
            If MessageType = "E" Then
                Cells(i, 11) = session.findById("wnd[0]/sbar").Text
                GoTo next_record
            ElseIf MessageType = "W" Then
                session.findById("wnd[0]").sendVKey 0
            End If
            
            MessageType = session.findById("wnd[0]/sbar").MessageType
            If MessageType = "E" Then
                Cells(i, 11) = session.findById("wnd[0]/sbar").Text
                GoTo next_record
            End If
            
            Total_items = session.findById("wnd[0]/usr").findByNameEx("SAPMV50ATC_LIPS_PICK", 80).VerticalScrollbar.Maximum + 1  '
            If Total_items = 0 Then Total_items = 1
            PageSize = session.findById("wnd[0]/usr").findByNameEx("SAPMV50ATC_LIPS_PICK", 80).VerticalScrollbar.PageSize
            If picking_view_selected = False Then
                session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_OVERVIEW/tabpT\02").Select
                picking_view_selected = True
            End If
            deleted_rows = 0
            For j = 1 To Total_items
                pageindex = (j - deleted_rows) Mod PageSize
                If pageindex = 1 Then
                    If j > 1 Then session.findById("wnd[0]/tbar[0]/btn[82]").press   ' click the next page button
                    currentrow = 0
                Else
                    currentrow = currentrow + 1
                End If
                Set cur_row = session.findById("wnd[0]/usr").findByNameEx("SAPMV50ATC_LIPS_PICK", 80).Rows(currentrow)
                If cur_row(0).Text = "" Then Exit For
                item_in_excel = False
                For k = i To last_row
                    If Cells(k, 2) <> Cells(i, 2) Then Exit For   'exit after reaching the next sales order
                    material = Cells(k, 4)
                    If cur_row(0).Text = CStr(Cells(k, 3)) And (material = CStr(Cells(k, 4)) Or Right(material, 7) = CStr(Cells(k, 4))) Then
                        If cur_row(3).Text = "" Then  'storage location
                            cur_row(3).Text = Cells(i, 6) '"R309"
                            session.findById("wnd[0]").sendVKey 0
                            Cells(k, 15) = session.findById("wnd[0]/sbar").Text
                            If session.findById("wnd[0]/sbar").MessageType = "E" Then Exit For
                        End If
                        cur_row(4).Text = Cells(k, 7)  'delivery qty
                        If cur_row(6).changeable = True Then cur_row(6).Text = Cells(k, 7)  'non lean warehouse location, manual input pick qty
                        Cells(k, 9) = Cells(k, 7) 'mark the excel row as actually delivered,
                        item_in_excel = True
                        item_count = item_count + 1
                        Exit For
                    End If
                Next k
                If item_in_excel = False Then
                    session.findById("wnd[0]/usr").findByNameEx("SAPMV50ATC_LIPS_PICK", 80).getAbsoluteRow(currentrow).Selected = True
                    session.findById("wnd[0]/usr").findByNameEx("BT_POLO_T", 40).press 'delete
                    session.findById("wnd[1]/usr/btnSPOP-OPTION1").press  ' confirm delete
                    currentrow = currentrow - 1
                    deleted_rows = deleted_rows + 1
                End If
            Next j
            If item_count > 0 Then
                session.findById("wnd[0]/tbar[0]/btn[11]").press  ' Save DN, auto pick in the backend
                If session.findById("wnd[0]/sbar").MessageType = "W" Then session.findById("wnd[0]").sendVKey 0
                Result = session.findById("wnd[0]/sbar").Text
                Cells(i, 11) = Result
                If InStr(Result, "has been saved") > 0 Then
                    array1 = Split(Result, " ")
                    Cells(i, 10) = array1(UBound(array1) - LBound(array1) - 3)     'extract delivery number from status text "FZ delivery 3333330 has been saved"
                End If
            Else
                Cells(i, 11) = "No DN created due to no valid items!"
            End If
       End If
next_record:
    Next i
    If batch_mode = False Then Call close_sap
End Sub

Sub batch_PGI()
    If batch_mode = False Then Call open_sap
    last_row = ActiveSheet.UsedRange.Rows.Count
    For i = 2 To last_row
        If Cells(i, 10) <> "" And Cells(i, 10) <> Cells(i - 1, 10) Then 'process sales order only once
            session.findById("wnd[0]/tbar[0]/okcd").Text = "/nvl02n"
            session.findById("wnd[0]").sendVKey 0
            session.findById("wnd[0]/usr/ctxtLIKP-VBELN").Text = Cells(i, 10) '"223176880"
            session.findById("wnd[0]/tbar[1]/btn[20]").press    'click Post Goods Issue button
            Result = session.findById("wnd[0]/sbar").Text
            For j = 1 To 10
                If InStr(1, Result, "processed by another user") > 0 Then   'wait till the previous SAVE finished
                    Application.Wait (Now + TimeValue("0:00:01"))
                    session.findById("wnd[0]/tbar[1]/btn[20]").press    'click Post Goods Issue button
                    Result = session.findById("wnd[0]/sbar").Text
                Else
                    Exit For
                End If
            Next j
            Cells(i, 12) = Result
        End If
    Next i
    If batch_mode = False Then Call close_sap
End Sub

Sub batch_print()
    Dim Filename As String
    If batch_mode = False Then Call open_sap
    last_row = ActiveSheet.UsedRange.Rows.Count
    For i = 2 To last_row   'output to local PDF printer, save the PDF file by delivery_number_PO.pdf
        If Cells(i, 10) <> "" And Cells(i, 10) <> Cells(i - 1, 10) Then 'process sales order only once
            session.findById("wnd[0]").maximize
            session.findById("wnd[0]/tbar[0]/okcd").Text = "/nvl02n"
            session.findById("wnd[0]").sendVKey 0
            session.findById("wnd[0]/usr/ctxtLIKP-VBELN").Text = Cells(i, 10) '"223176880"
            session.findById("wnd[0]").sendVKey 0
            session.findById("wnd[0]/mbar/menu[3]/menu[1]/menu[0]").Select
            session.findById("wnd[0]/usr/tblSAPDV70ATC_NAST3").getAbsoluteRow(0).Selected = True
            session.findById("wnd[0]/tbar[1]/btn[5]").press
            session.findById("wnd[0]/usr/cmbNAST-VSZTP").Key = "4"
            session.findById("wnd[0]/tbar[0]/btn[3]").press
            session.findById("wnd[0]/tbar[0]/btn[11]").press
            Filename = CStr(Cells(i, 10)) & "_" & Cells(i, 1)
            If Sheets(2).Cells(2, 2) <> "" Then Filename = Sheets(2).Cells(2, 2) & Filename
            For j = 1 To 10  'wait 10 seconds
                If Module2.Auto_SaveAs_SAP(Filename) = "OK" Then Exit For
                Application.Wait (Now + TimeValue("0:00:01"))
                Sleep 1000
            Next j
        End If
    Next i
    If batch_mode = False Then Call close_sap
End Sub

Sub batch_create_billing()
    If batch_mode = False Then Call open_sap
    last_row = ActiveSheet.UsedRange.Rows.Count
    For i = 2 To last_row
        If Cells(i, 10) <> "" And Cells(i, 10) <> Cells(i - 1, 10) Then 'process sales order only once
            session.findById("wnd[0]/tbar[0]/okcd").Text = "/nvf01"
            session.findById("wnd[0]").sendVKey 0
            session.findById("wnd[0]/usr/tblSAPMV60ATCTRL_ERF_FAKT/ctxtKOMFK-VBELN[0,0]").Text = Cells(i, 10) '"223176880"
            session.findById("wnd[0]/tbar[0]/btn[11]").press
            Cells(i, 13) = session.findById("wnd[0]/sbar").Text
        End If
    Next i
    If batch_mode = False Then Call close_sap
End Sub

Sub allocate_material()
    Sheets("template").Activate
    last_row = Sheets("template").UsedRange.Rows.Count
    For j = 2 To last_row   'initialize allocated qty as 0
        Sheets("template").Cells(j, 7) = 0
    Next j
    
    For i = 2 To Sheets("MB52").UsedRange.Rows.Count
        material = Sheets("MB52").Cells(i, 1)
        If Left(material, 1) = "0" Then
            material_7 = Right(material, 7)
        Else
            material_7 = material
        End If
        Location = Sheets("MB52").Cells(i, 2)
        available_qty = Sheets("MB52").Cells(i, 3)
        allocated_qty = 0
        balance = available_qty
        For j = 2 To last_row
            If Sheets("MB52").Cells(i, 3) - allocated_qty <= 0 Then Exit For
            required_allocate_qty = Sheets("template").Cells(j, 5) - Sheets("template").Cells(j, 7)
            If (Sheets("template").Cells(j, 4).Text = material Or Sheets("template").Cells(j, 4).Text = material_7) _
                And Sheets("template").Cells(j, 6).Text = Location And required_allocate_qty > 0 Then
                If balance < required_allocate_qty Then required_allocate_qty = balance
                Sheets("template").Cells(j, 7) = Sheets("template").Cells(j, 7) + required_allocate_qty
                allocated_qty = allocated_qty + required_allocate_qty
                balance = available_qty - allocated_qty
            End If
        Next j
        Sheets("MB52").Cells(i, 4) = allocated_qty
        Sheets("MB52").Cells(i, 5) = available_qty - allocated_qty
    Next i
End Sub

Sub download_mb52()
    Dim wbk As Workbook
    Dim wbk1 As Workbook
    Dim Filename As String
    Dim Path As String
    'initialize
    Set wbk1 = ThisWorkbook
    Set sht_mb52 = wbk1.Sheets("MB52")
    sht_mb52.UsedRange.ClearContents
    sht_mb52.Cells(1, 1) = "Material"
    sht_mb52.Cells(1, 2) = "Storage Location"
    sht_mb52.Cells(1, 3) = "Unrestricted"
    sht_mb52.Cells(1, 4) = "Allocated"
    sht_mb52.Cells(1, 5) = "Balance"
        
    Sheets("template").Activate
    last_row = ActiveSheet.UsedRange.Rows.Count
    Range("D2:D" + CStr(last_row)).Select
    Selection.Copy
    
    If batch_mode = False Then Call open_sap
    session.findById("wnd[0]").maximize
    session.findById("wnd[0]/tbar[0]/okcd").Text = "/nmb52"
    session.findById("wnd[0]").sendVKey 0
    
    Range("G2:G" + CStr(last_row)).Select   'material
    Selection = 0
    session.findById("wnd[0]/usr/btn%_MATNR_%_APP_%-VALU_PUSH").press
    session.findById("wnd[1]/tbar[0]/btn[16]").press   'delete previous input
    session.findById("wnd[1]/tbar[0]/btn[24]").press
    session.findById("wnd[1]/tbar[0]/btn[8]").press
    
    session.findById("wnd[0]/usr/ctxtWERKS-LOW").Text = "30r3"
    
    Range("F2:F" + CStr(last_row)).Select   'storage location
    Selection.Copy
    session.findById("wnd[0]/usr/btn%_LGORT_%_APP_%-VALU_PUSH").press
    session.findById("wnd[1]/tbar[0]/btn[16]").press   'delete previous input
    session.findById("wnd[1]/tbar[0]/btn[24]").press
    session.findById("wnd[1]/tbar[0]/btn[8]").press
    
    session.findById("wnd[0]/usr/chkNOZERO").Selected = True
    session.findById("wnd[0]/usr/chkNOVALUES").Selected = True
    'session.findById("wnd[0]/usr/ctxtP_VARI").SetFocus
    
    session.findById("wnd[0]/tbar[1]/btn[8]").press
    
    session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell").selectedRows = "0"
    session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell").contextMenu
    session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell").selectContextMenuItem "&XXL"
    session.findById("wnd[1]/usr/radRB_OTHERS").Select
    session.findById("wnd[1]/usr/cmbG_LISTBOX").Key = "10"
    session.findById("wnd[1]/tbar[0]/btn[0]").press
    Path = Sheets(2).Cells(2, 2) '"D:\test\"
    Filename = "MB52" & "_" & Replace(CStr(CDbl(Now())), ".", "") & ".xlsx"
    For ii = 1 To 10
        On Error Resume Next
            session.findById("wnd[1]/usr/ctxtDY_PATH").Text = Path '"d:\sap_upload\"
            session.findById("wnd[1]/usr/ctxtDY_FILENAME").Text = Filename  '"book1.xlsx"
        If Err.Number <> 0 Then
            Application.Wait (Now + TimeValue("0:00:01"))
        Else
            Exit For
        End If
    Next ii
    session.findById("wnd[1]/tbar[0]/btn[0]").press
    On Error Resume Next
        Set wbk = GetObject(Path & Filename)
    On Error GoTo 0
    'if it's not already opened - open it
    If wbk Is Nothing Then Set wbk = Workbooks.Open(Path & Filename)
    Set sht = wbk.Sheets(1)
    'extract the unrestricted stock quantity only
    last_col = sht.UsedRange.Columns.Count
    last_row = sht.UsedRange.Rows.Count
    col_count = 0
    For i = 1 To last_col
        If sht.Cells(1, i).Text = "Material" Then
            col_material = i
            col_count = col_count + 1
        ElseIf sht.Cells(1, i).Text = "Storage Location" Then
            col_location = i
            col_count = col_count + 1
        ElseIf sht.Cells(1, i).Text = "Unrestricted" Then
            col_available_qty = i
            col_count = col_count + 1
        End If
        If col_count >= 3 Then Exit For
    Next i
    If col_count < 3 Then
        MsgBox "MB52 layout should include at least 3 mandatory fields: material, storage location, unrestricted"
        Exit Sub
    End If
    For j = 2 To last_row
        sht_mb52.Cells(j, 1) = sht.Cells(j, col_material)
        sht_mb52.Cells(j, 2) = sht.Cells(j, col_location)
        sht_mb52.Cells(j, 3) = sht.Cells(j, col_available_qty)
    Next j
    wbk.Close savechanges:=False
    wbk1.Activate
    If batch_mode = False Then Call close_sap
End Sub

Sub allocate_qty()
    Application.ScreenUpdating = False
    Application.DisplayAlerts = False
    batch_mode = True
    Call open_sap
    Call download_mb52
    Call allocate_material
    Application.DisplayAlerts = True
    Application.ScreenUpdating = True
    Call close_sap
End Sub
